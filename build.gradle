buildscript {
    ext {
        springBootVersion = '1.3.1.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'groovy'

apply plugin: 'spring-boot'

archivesBaseName = 'archive-card-service-2'

group = 'main'
version = '0.1.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}



task writeCloudFoundryManifest(type: Copy) {
    from 'cloud-foundry'
    into "${buildDir}"
    include 'manifest.yml.template'
    rename { file -> 'manifest.yml' }
    expand([
            stormpath_apiKey_id: "$System.env.stormpath_apiKey_id",
            stormpath_apiKey_secret: "$System.env.stormpath_apiKey_secret",
            deployment_environment: "$System.env.deployment_environment"
    ])
}

//task cfPush(dependsOn:[writeCloudFoundryManifest,cfLogin], type:Exec){
task cfPush(dependsOn:[], type:Exec){
    commandLine "cf","push","-f","${buildDir}/manifest.yml"
}



dependencies {
    compile 'com.stormpath.spring:stormpath-default-spring-boot-starter:1.0.RC6'
    compile('org.springframework.boot:spring-boot-starter-jdbc')
    compile('org.postgresql:postgresql')

    testCompile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.1.5'
    testCompile group: 'org.codehaus.groovy.modules.http-builder', name: 'http-builder', version: '0.7.1'
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
}

sourceSets {
    integrationTest {
        java {
            srcDirs = ['src/main/java']
        }

        groovy {
            srcDirs = ['src/integrationTest/groovy']
        }

        resources.srcDir file('src/integrationTest/resources')
        compileClasspath = sourceSets.main.output + configurations.testRuntime
        runtimeClasspath = output + compileClasspath
    }
}


task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    systemProperties = System.properties
}


